pkgname="kakoune-nightly-lenormf-debug"
pkgver=latest
pkgrel=1
pkgdesc="An experiment for a better code editor by mawww"
options=(!buildflags !debug !strip)
arch=("i686" "x86_64")
url="https://github.com/mawww/kakoune"
license=("custom:unlicense")
conflicts=("kakoune-git" "kakoune-nightly" "kakoune-nightly-debug" "kakoune-nightly-lenormf")
depends=("ncurses" "boost")
optdepends=("xorg-xmessage: for nicer debugging messages"
            "xdotool: add support for kak windows switching (in grep, make... files)"
            "clang: add command for C/CPP insert mode completion support"
            "universal-ctags-git: provides \`readtags\` used by \`:tag\` command to jump on a tag definition"
            "tmux: tabbing support")
makedepends=("git" "asciidoc" "ccache")
checkdepends=("diffutils" "findutils" "sed")
source=(
    "${pkgname}::git+https://github.com/mawww/kakoune.git"
    "001-pr-buflist.patch::https://github.com/mawww/kakoune/pull/1065.patch"
    "002-pr-prompt-readline.patch::https://github.com/mawww/kakoune/pull/800.patch"
)
sha256sums=(
    "SKIP"
    "879dee73c6a5af572f04a5ca0a13363c9a482acc350d7c2960b995ee75fda5be"
    "cca1d4b70c721addcbf8b8dc981af7031e8d9013012e5d5b265536595d22cb20"
)

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

check() {
    cd "${srcdir}/${pkgname}/src"
    make test
}

build() {
    cd "${srcdir}/${pkgname}"

    for patch in "${source[@]}"; do
        patch="${srcdir}/${patch%%::*}"

        if [ -f "${patch}" ]; then
            patch -Np1 -i "${patch}"
        fi
    done

    cd src
    make CXX=/usr/lib/ccache/bin/g++ debug=yes
}

package() {
    cd "${srcdir}/${pkgname}/src"
    make install DESTDIR="${pkgdir}" PREFIX=/usr CXX=/usr/lib/ccache/bin/g++ debug=yes

    install -D ../UNLICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
